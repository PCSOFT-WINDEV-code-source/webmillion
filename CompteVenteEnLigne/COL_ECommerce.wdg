#To edit and compare internal_properties, use WINDEV integrated tools.
#Internal properties refer to the properties of controls in windows, reports, etc.
info :
 name : COL_ECommerce
 major_version : 28
 minor_version : 0
 type : 7
 description : ""
 subtype : 0
procedure_set :
 identifier : 0x1e0c4c3d00049bd8
 internal_properties : CAAAAAgAAABGLu41kG7fjQV3iS4F72qmnKaNh5694reolNKIW0iw
 code_elements :
  internal_properties : CAAAAAgAAACEvY6oxe0erTMxwaC13zKBPYVw2flBVoHWPyKFzRvq1FZL9E+FZdvqYasBHKFB4KjX9RbIJ+0AEvqUQjw4Bg4A3SLbGqOiQXl7KNvEtJRqHEXfbqEnMPCoxDzLDLK3gaNxOzUObTf9dIvEMd+WibsdnhvkAe6iA/YSGx5/TfXq3HOSSvX8KCdpQvtHe0nS/jujHfVhr2u/wHWCk19+P+ShnlfPTsIMQlz15YO26f2ccSXWMSIVe17OdViuxf1bKjIfEetfi7ZOF3XobUgcVt0WRuRW6KkHQ+fUCusp1iiNWCOg7hTslQLF4x9/MnifyFVudkAg09DxT9TX6+NGCYSuJppNkJ+iqR2jL0+t/IZBvdNMFGL++Hhr2qXh1KcboNH14WR/bqHe8rTnDEuHd/W76z6aigtKsK05Qg8UMIaltIBcBQdsf1xr3bvn2Z2J8w==
  type_code : 31
  p_codes :
   -
     internal_properties : CAAAAAgAAACKBLZKY7g8QfAy6jRQV00vhmed06pTzj6O/5beq1YZWshDLv6SCR8iwXt6NETfYs5Fmel+1l7YQrhh1bTzo/nZ7nhJ3EKGRddJCZZPXKeIfDdDRZnsrhMIpN5qLfNB0bcwy5jMD+9FXlhKtq2Atf9XBi2h53pHzEUU6dn8cbX83bKs5iP0svPNv9T6ULCP7Xdfxa5xYt3JRo4Dw5o7N3JfRSUYNWfgw/DhBfN71sYCBTYRSPLqOyzAU/FX7Y0d5nahzX4iR2/Cx5jH92kuj0XG
     code : |1-
      // La collection de procédures COL_ECommerce sert de base à la gestion d'un site de commerce électronique
      // Elle utilise les fichiers suivants de l'analyse :
      // - Client : fichier des comptes clients
      // - Adresse : fichiers des adresses actives pour les clients
      // - Commande : fichier des commandes passées sur le site
      // - LigneCde : fichier du détail des commandes
      // - AdresseArchive : fichier des adresses utilisées dans les commandes, en ajout seulement
      // - Produit : fichier des produits
      // - FamilleProduit : fichier des familles de produits
      // - PhotoProduit : fichier des photos des produits
      // - TauxTaxe : fichier des taux de taxe
      // Le projet doit obligatoirement utiliser la gestion des contextes AWP sur le disque (fonction ConfigureContexteAWP(ctxDisque) dans le code d'initialisation du projet)
      
      // Constantes du composant interne
      constant
      	// Types de règlements (utilisé dans la rubrique Commande.TypeReglement)
      	REGL_CB = 1				// Règlement par carte bancaire
      	REGL_PAYPAL = 2			// Règlement par PayPal
      	REGL_VIREMENT = 3		// Règlement par virement bancaire
      	REGL_CHEQUE = 4			// Règlement par chèque
      	
      	// Etats d'une commande (utilisé dans la rubrique Commande.EtatCommande)
      	CDE_ATTENTEREGLEMENT = 1		// Commande en attente de règlement
      	CDE_PREPARATION = 2				// Commande réglée, en cours de préparation
      	CDE_LIVRAISON = 3				// Commande en cours de livraison
      	CDE_LIVREE = 4					// Commande livrée
      	
      	// Pages par défaut
      	PAGE_VISUPANIER_DEFAUT = "panier.awp"
      	PAGE_RETOUR_BOUTIQUE_DEFAUT = "accueil.awp"
      	PAGE_VISU_PRODUIT_DEFAUT = "produit.awp"
      	
      	// Rôles des photos des produits
      	ROLE_PHOTO_NORMAL = 0		// Photo normale
      	ROLE_PHOTO_PANIER_PETIT = 1	// Miniature pour les petits panier (32x32 en général)
      	ROLE_PHOTO_PANIER_GRAND = 2	// Miniature pour les grands paniers (64x64 en général)
      FIN
      
      // Structures de panier client
      STLignePanier est une structure
      	// Contenu de la ligne de panier
      	sRefProduit est une chaine				// Référence du produit
      	nNbArticle est un entier				// Nombre d'articles de cette référence
      	// Variables "cache" pour limiter le nombre de calcul/accès aux fichiers sur le serveur
      	sLibArticle est une chaine				// Libellé de l'article
      	moPrixUnitaireHT est un monétaire		// Prix unitaire HT
      	moTaxeUnitaire est un monétaire			// Montant de la taxe appliquée à un article
      	moPrixUnitaireTTC est un monétaire		// Prix unitaire TTC
      FIN
      
      STCommande est une structure
      	// Contenu du panier
      	tabPanier est un tableau de STLignePanier
      	// Variables "cache" pour limiter le nombre de calculs sur le serveur
      	taProduit est un tableau associatif de entier		// Rang d'un produit dans le panier (indexé sur sa référence)
      	nNbArticle est un entier							// Nombre d'articles
      	moPrixTotalHT est un monétaire						// Valeur totale du panier (HT)
      	moTaxeTotal est un monétaire						// Valeur totale des taxes
      	// Variables de livraison
      	moFraisDeLivraisonHT est un monétaire				// Frais de livraison (HT)
      	moFraisDeLivraisonTTC est un monétaire				// Frais de livraison (TTC)
      	// Valeur totale du panier TTC, y compris les frais de livraison (montant à payer)
      	moPrixTotalTTC est un monétaire						// Valeur totale du panier (TTC)
      	
      	// Variables utilisées par le tunnel de conversion
      	nIDClient est un entier				// Client en cours de commande
      	nIDAdresseLivraison est un entier	// Adresse de livraison sélectionnée
      	nIDAdresseFacturation est un entier	// Adresse de facturation sélectionnée
      	
      	nIDCommande est un entier			// Commande en cours de création
      	
      	nTypePort est un entier // Type de port choisi
      FIN
      
      // Variables globales du composant interne (ces variables sont placées dans le contexte AWP dans la procédure InitialiseECommerce)
      gbInitTerminée est un booleen = faux				// Vrai si l'init du composant interne a déjà été effectuée
      gStPanier est un STCommande							// Variable mémorisant le panier
      gsNomSite est une chaine							// Nom du site pour l'affichage dans les pages de commande
      gbConnexionPayBoxSSL est un booléen = Vrai			// Vrai si la connexion à PayBox (paiement CB) doit se faire en SSL
      gsErreurEnCours est une chaine						// Texte contenant la dernière erreur fatale
      gsFichierCGV est une chaine							// Nom du fichier contenant les Conditions Générales de Vente (texte simple ou HTML)
      gsEmailConfirmationCommande est une chaine = "servicecommande"	// Adresse expéditeur des emails de confirmation de commande
      
      // Points d'entrées personnalisables
      gPCalculFraisDePort est une procédure = FraisDePortDefaut			// Procédure permettant de calculer les frais de port pour un panier rempli
      gPInitModulePaiement est une procédure = InitModulePaiementDefaut 	// Procédure d'initialisation des modules de paiement (PayBox, PayPal, etc.)
      gPGenereNumCommande est une procédure = CommandeCrée			// Procédure renvoyant à chaque appel le prochain numéro de commande à utiliser
      gPEmailConfirmationCommande est une procédure = CommandeEmailConfirmationDefaut // Procédure envoyant les emails de confirmation de commande
      
      // Noms de pages
      gsNomPageVisuPanier est une chaine = PAGE_VISUPANIER_DEFAUT		// Page de visualisation du panier (entrée du tunnel de conversion)
      gsNomPageRetourBoutique est une chaine = PAGE_RETOUR_BOUTIQUE_DEFAUT	// Page de retour à la boutique depuis le tunnel de conversion
      gsNomPageVisuProduit est une chaine = PAGE_VISU_PRODUIT_DEFAUT	// Page de visualisation d'un produit
      
      // Textes de la page de paiement
      gsTextePaiementCB est une chaine = <§@1e0c492300017dcb0000§>
      gsTextePaiementPayPal est une chaine = <§@1e0c492300017dcb0001§>
      gsTextePaiementVirement est une chaine = <§@1e0c492300017dcb0002§>
      gsTextePaiementCheque est une chaine = <§@1e0c492300017dcb0003§>
      
      // Variables pour la gestion du paiement
      gsURLRetourBoutique est une chaine
      gsURLPaiementOK est une chaine
      gsURLPaiementAbandon est une chaine
      gsURLPaiementEchec est une chaine
      gsPaiementNomObjet est une chaine
      gsPaiementRefObjet est une chaine
      
      gbPaiementModeSimulation est un booléen
      gsPayPalIdentifiant, gsPayPalMotDePasse, gsPayPalSignature sont des chaines
      gsPayBoxContrat est une chaine 
      gsPayBoxCodeSociété, gsPayBoxRang sont des entiers
      
      
      
      // 
      gsSeparateurDecimal est une chaine = NumériqueVersChaîne(1.1,"1,2f")[[2]]
      gnNbDécimales est un entier = 3 // Par défaut 3 décimales
     type : 720896
  procedures :
   -
     name : InitialiseECommerce
     internal_properties : CAAAAAgAAADFcA6Z0EHb5LC/Ok82Z50A8M3sDZ/R4JFlwNFli599AAPdkQQYhWXA8hHWDypfQac0A4uLjBftti+8RNoiIRpzJ8GRHDBEdEcFQ3puo37j5DYTYBmHpeFyevFYwu3xbiHDz28d/RDmPuw4EDsYWzB7kqvUP8UnC8IB7xYkTKEsdJIV+qXSfqFYtW5Sbw7AyJCB
     procedure_id : 2165193327170207594
     type_code : 15
     code : |1+
      // Résumé : Initialise le composant interne
      // Syntaxe :
      //[ <Résultat> = ] InitialiseECommerce ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	booléen : // Retourne Vrai lors de la première initialisation
      // L'appelant sait alors que les variables de la collection de procédures ont leur valeurs par défaut et qu'il peut les remplacer
      //
      // Retourne Faux lors des initialisations suivantes (après une relecture de contexte AWP)
      // L'appelant sait alors que les variables ont été relues dans la session AWP
      //
      PROCEDURE InitialiseECommerce()
      
      // Déclaration des variables globales du contexte
      DéclareContexteAWP(gbInitTerminée)
      DéclareContexteAWP(gStPanier)
      DéclareContexteAWP(gsNomSite)
      DéclareContexteAWP(gsNomPageVisuPanier)
      DéclareContexteAWP(gsNomPageRetourBoutique)
      DéclareContexteAWP(gsTextePaiementCB)
      DéclareContexteAWP(gsTextePaiementPayPal)
      DéclareContexteAWP(gsTextePaiementVirement)
      DéclareContexteAWP(gsTextePaiementCheque)
      DéclareContexteAWP(gbConnexionPayBoxSSL)
      DéclareContexteAWP(gsErreurEnCours)
      DéclareContexteAWP(gsFichierCGV)
      DéclareContexteAWP(gsURLPaiementOK)
      DéclareContexteAWP(gsURLRetourBoutique)
      DéclareContexteAWP(gsURLPaiementAbandon)
      DéclareContexteAWP(gsURLPaiementEchec)
      DéclareContexteAWP(gsPaiementNomObjet)
      DéclareContexteAWP(gsPaiementRefObjet)
      
      DéclareContexteAWP(gbPaiementModeSimulation)
      DéclareContexteAWP(gsPayPalIdentifiant)
      DéclareContexteAWP(gsPayPalMotDePasse)
      DéclareContexteAWP(gsPayPalSignature)
      DéclareContexteAWP(gsPayBoxContrat)
      DéclareContexteAWP(gsPayBoxCodeSociété)
      DéclareContexteAWP(gsPayBoxRang)
      
      
      DéclareContexteAWP(gsEmailConfirmationCommande)
      DéclareContexteAWP(gsNomPageVisuProduit)
      
      // Initialisation des modules de paiement
      // On le fait systématiquement parce que la procédure doit initialiser des variables internes au composant paiement sécurisé qui ne sont pas mémorisées (pour des raisons de sécurité)
      COL_ECommerce.gPInitModulePaiement()
      
      // Si c'est la première init (les variables n'ont pas été lues dans le contexte)
      SI gbInitTerminée = Faux ALORS
      	// L'init est terminée
      	gbInitTerminée = Vrai
      	
      	// Par défaut, le nom du site est le nom de déploiement
      	gsNomSite = ProjetInfo(piNomSiteDéployé)
      	gsFichierCGV = "cgv.html"
      
      	// Retourne Vrai lors de la première initialisation
      	// L'appelant sait alors que les variables de la collection de procédures ont leur valeurs par défaut et qu'il peut les remplacer
      	RENVOYER Vrai
      FIN
      
      // Retourne Faux lors des initialisations suivantes (après une relecture de contexte AWP)
      // L'appelant sait alors que les variables ont été relues dans la session AWP
      RENVOYER Faux
      
     type : 458752
   -
     name : PanierNbArticle
     internal_properties : CAAAAAgAAABk2piQTHc9frUYXC8sGrFNdcD6A/V8pr7a2vCJUrrLdXKYnWF8rhmA8NMIXOr9X7Ar7TD9Wujp9jHrkBgEgeqfpH9wUx6NSVSV/qWEdwJENj3QB7p0JHM=
     procedure_id : 2165284363300121939
     type_code : 15
     code : |1-
      // Résumé : Retourne un libellé correspondant au nombre d'articles dans le panier
      // Syntaxe :
      //[ <Résultat> = ] PanierNbArticle ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	chaîne : // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      PROCEDURE PanierNbArticle()
      
      selon gStPanier.nNbArticle
      	cas 0
      		renvoyer "Panier vide"
      	cas 1
      		renvoyer "1 article"
      FIN
      
      renvoyer ChaîneConstruit("%1 articles",gStPanier.nNbArticle)
     type : 458752
   -
     name : PanierValeurTTC
     internal_properties : CAAAAAgAAABk2piQTHc9frUYXC8sGrFNdcD6A/V8pr7a2vCJUrrLdXKYnWF8rhmA8NMIXOr9X7Ar7TD9Wujp9jHrkBgEgeqfpH9wUx6NSVSV/qWEdwJENj3QB7p0JHM=
     procedure_id : 2165286167186479001
     type_code : 15
     code : |1-
      // Résumé : Retourne la valeur totale du panier (TTC)
      // Syntaxe :
      //[ <Résultat> = ] PanierValeurTTC ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	chaîne : // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      PROCEDURE PanierValeurTTC()
      
      renvoyer MonétaireVersChaine(gStPanier.moPrixTotalTTC)
     type : 458752
   -
     name : PanierLibelleLigne
     procedure_id : 2165290015477481706
     type_code : 15
     code : |1-
      // Résumé : Retourne un libellé correspondant à la ligne du panier
      // Syntaxe :
      //[ <Résultat> = ] PanierLibelleLigne (<nLigne> est entier)
      //
      // Paramètres :
      //	nLigne (entier) : <indiquez ici le rôle de nLigne>
      // Valeur de retour :
      // 	variant : // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      PROCEDURE PanierLibelleLigne( nLigne est un entier )
      
      dbgAssertion(nLigne<=TableauOccurrence(gStPanier.tabPanier))
      
      stLigne est un STLignePanier = gStPanier.tabPanier[nLigne]
      
      renvoyer ChaîneConstruit("%1 %2 (%3)",stLigne.nNbArticle,stLigne.sLibArticle,MonétaireVersChaine(stLigne.moPrixUnitaireTTC*stLigne.nNbArticle))
     type : 458752
   -
     name : PanierAjoute
     internal_properties : CAAAAAgAAABk2piQTHc9frUYXC8sGrFNdcD6A/V8pr7a2vCJUrrLdXKYnWF8rhmA8NMIXOr9X7Ar7TD9Wujp9jHrkBgEgeqfpH9wUx6NSVSV/qWEdwJENj3QB7p0JHM=
     procedure_id : 2166670031416594321
     type_code : 15
     code : |1+
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      //[ <Résultat> = ] PanierAjoute (<sRefProduit> est chaîne, <nNbAjout> est entier)
      //
      // Paramètres :
      //	sRefProduit (chaîne) : <indiquez ici le rôle de ATT_LIB_IDProduit>
      //	nNbAjout (entier) : <indiquez ici le rôle de ATT_NbAjout>
      // Valeur de retour :
      // 	booléen : // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      PROCEDURE PanierAjoute( sRefProduit est une chaine, nNbAjout est un entier )
      
      renvoyer PanierModifieNombre(sRefProduit,nNbAjout)
     type : 458752
   -
     name : PanierSupprimeProduit
     internal_properties : CAAAAAgAAABk2piQTHc9frUYXC8sGrFNdcD6A/V8pr7a2vCJUrrLdXKYnWF8rhmA8NMIXOr9X7Ar7TD9Wujp9jHrkBgEgeqfpH9wUx6NSVSV/qWEdwJENj3QB7p0JHM=
     procedure_id : 2166739231938571403
     type_code : 15
     code : |1+
      // Résumé : Supprime un produit du panier (quelque soit la quantité présente)
      // Syntaxe :
      //PanierSupprimeProduit (<sRefProduit> est chaîne)
      //
      // Paramètres :
      //	sRefProduit (chaîne) : Référence à supprimer
      
      PROCEDURE PanierSupprimeProduit( sRefProduit est une chaine )
      
      dbgAssertion(gStPanier.taProduit[sRefProduit]>0,"Suppression d'un article inexistant")
      nRangDansPanier est un entier = gStPanier.taProduit[sRefProduit]
      
      // Actualise les compteurs globaux
      gStPanier.nNbArticle -= gStPanier.tabPanier[nRangDansPanier].nNbArticle
      gStPanier.moPrixTotalTTC -= gStPanier.tabPanier[nRangDansPanier].nNbArticle*gStPanier.tabPanier[nRangDansPanier].moPrixUnitaireTTC
      gStPanier.moPrixTotalHT -= gStPanier.tabPanier[nRangDansPanier].nNbArticle*gStPanier.tabPanier[nRangDansPanier].moPrixUnitaireHT
      gStPanier.moTaxeTotal -= gStPanier.tabPanier[nRangDansPanier].nNbArticle*gStPanier.tabPanier[nRangDansPanier].moTaxeUnitaire
      
      // Supprime dans les tableaux
      TableauSupprimeLigne(gStPanier.tabPanier,nRangDansPanier)
      TableauSupprimeLigne(gStPanier.taProduit,sRefProduit)
      
      
      
      // Actualise les index dans le tableau associatif
      pour tout nRang de gStPanier.taproduit
      	si nRang>nRangDansPanier ALORS
      		nRang--
      	FIN
      FIN
     type : 458752
   -
     name : PanierSupprime
     procedure_id : 2166764353206495651
     type_code : 15
     code : |1+
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      //[ <Résultat> = ] PanierSupprime (<sRefProduit> est chaîne, <nNbSuppr> est entier)
      //
      // Paramètres :
      //	sRefProduit (chaîne) : <indiquez ici le rôle de sRefProduit>
      //	nNbSuppr (entier) : <indiquez ici le rôle de nNbAjout>
      // Valeur de retour :
      // 	booléen : // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      PROCEDURE PanierSupprime( sRefProduit est une chaine, nNbSuppr est un entier )
      
      renvoyer PanierModifieNombre(sRefProduit,-nNbSuppr)
     type : 458752
   -
     name : PanierModifieNombre
     internal_properties : CAAAAAgAAACfxbj8zu8TMrxuyCpzKefu2mwx94f9OkEP3Sp11dr2XzVGHDtxZ9wwVt8LlA+TWs+XVJt+b0QS0NeubsoxzT9SSKSSKPeMihd3uQdqmgZtIZvXj5lfjbx5c20v9jmgd4UDnTgXhp6RArhatxFfHY+S
     procedure_id : 2166764460580769259
     type_code : 15
     code : |1-
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      //[ <Résultat> = ] PanierModifieNombre (<sRefProduit> est chaîne, <nNbModif> est entier [, <bSupprimeSiNul> est booléen])
      //
      // Paramètres :
      //	sRefProduit (chaîne) : <indiquez ici le rôle de sRefProduit>
      //	nNbModif (entier) : <indiquez ici le rôle de nNbAjout>
      //	bSupprimeSiNul (booléen - valeur par défaut=1) : <indiquez ici le rôle de bSupprimeSiNul>
      // Valeur de retour :
      // 	booléen : // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      PROCEDURE PanierModifieNombre(sRefProduit est une chaine, nNbModif est un entier , bSupprimeSiNul est un booleen = vrai )
      
      // Cherche le produit dans le panier
      nRangDansPanier est un entier = gStPanier.taProduit[sRefProduit]
      SI nRangDansPanier=0 ALORS
      	// Si on est en suppression, c'est une erreur
      	si nNbModif<0 alors
      		dbgAssertion(faux,"Suppression d'un produit qui n'est pas dans le panier")
      		renvoyer faux
      	FIN
      	
      	// Ce produit n'existe pas encore dans le panier, on l'ajoute
      
      	// Recherche le produit et son taux de taxe dans la base
      	SI HLitRecherchePremier(Produit,Reference,sRefProduit)=Faux ALORS
      		dbgAssertion(Faux,"On essaie d'ajouter un produit inconnu")
      		RENVOYER Faux
      	FIN
      	
      	SI HLitRecherchePremier(TauxTaxe,IDTauxTaxe,Produit.IDTauxTaxe) = Faux ALORS
      		dbgAssertion(Faux,"Le produit n'est pas associé à un taux de taxe")
      		RENVOYER Faux
      	FIN
      
      	// Création de la ligne de panier
      	stNouvelleLigne est une STLignePanier 
      	stNouvelleLigne.moPrixUnitaireHT = Produit.PrixUnitaireHT
      	stNouvelleLigne.moTaxeUnitaire = (Produit.PrixUnitaireHT * TauxTaxe.Taux) / 100
      	stNouvelleLigne.moPrixUnitaireTTC = stNouvelleLigne.moPrixUnitaireHT + stNouvelleLigne.moTaxeUnitaire
      	stNouvelleLigne.nNbArticle = nNbModif
      	stNouvelleLigne.sLibArticle = Produit.LibelleProduit
      	stNouvelleLigne.sRefProduit = sRefProduit
      	nRangDansPanier = TableauAjouteLigne(gStPanier.tabPanier,stNouvelleLigne)
      	dbgAssertion(nRangDansPanier<>-1,"Echec de l'ajout dans le tableau")
      	SI nRangDansPanier>0 ALORS
      		gStPanier.taProduit[sRefProduit] = nRangDansPanier
      	SINON
      		RENVOYER Faux
      	FIN
      SINON
      	// Le produit existe déjà dans le panier, on se contente de modifier le nombre
      	gStPanier.tabPanier[nRangDansPanier].nNbArticle += nNbModif
      FIN
      
      // Si le nombre d'élément est à 0, on retire l'article du panier
      si gStPanier.tabPanier[nRangDansPanier].nNbArticle=0 _et_ bSupprimeSiNul ALORS
      	PanierSupprimeProduit(sRefProduit)
      	renvoyer vrai
      FIN
      
      // Actualise les compteurs globaux
      gStPanier.nNbArticle += nNbModif
      gStPanier.moPrixTotalHT += nNbModif*gStPanier.tabPanier[nRangDansPanier].moPrixUnitaireHT
      gStPanier.moTaxeTotal += nNbModif*gStPanier.tabPanier[nRangDansPanier].moTaxeUnitaire
      gStPanier.moPrixTotalTTC += nNbModif*gStPanier.tabPanier[nRangDansPanier].moPrixUnitaireTTC
      
      RENVOYER Vrai
     type : 458752
   -
     name : PanierQuantite
     internal_properties : CAAAAAgAAABk2piQTHc9frUYXC8sGrFNdcD6A/V8pr7a2vCJUrrLdXKYnWF8rhmA8NMIXOr9X7Ar7TD9Wujp9jHrkBgEgeqfpH9wUx6NSVSV/qWEdwJENj3QB7p0JHM=
     procedure_id : 2167150771419152443
     type_code : 15
     code : |1-
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      //[ <Résultat> = ] PanierQuantite (<nRang> est entier)
      //
      // Paramètres :
      //	nRang (entier) : <indiquez ici le rôle de nRang>
      // Valeur de retour :
      // 	entier : // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      PROCEDURE PanierQuantite( nRang est un entier )
      
      renvoyer COL_ECommerce.gStPanier.tabPanier[nRang].nNbArticle
     type : 458752
   -
     name : MonétaireVersChaine
     internal_properties : CAAAAAgAAABk2piQTHc9frUYXC8sGrFNdcD6A/V8pr7a2vCJUrrLdXKYnWF8rhmA8NMIXOr9X7Ar7TD9Wujp9jHrkBgEgeqfpH9wUx6NSVSV/qWEdwJENj3QB7p0JHM=
     procedure_id : 2167395417038990332
     type_code : 15
     code : |1+
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      //[ <Résultat> = ] MonétaireVersChaine (<moValeur> est monétaire)
      //
      // Paramètres :
      //	moValeur (monétaire) : <indiquez ici le rôle de moValeur>
      // Valeur de retour :
      // 	chaîne ANSI : // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      //	nNbDécimales (entier - valeur par défaut=3) : <indiquez ici le rôle de nNbDécimales>
      PROCEDURE MonétaireVersChaine( local moValeur est un monétaire)
      
      renvoyer NumériqueVersChaîne(moValeur,maskMonétaireSystème)
      
     type : 458752
   -
     name : PanierTotalTaxe
     internal_properties : CAAAAAgAAABk2piQTHc9frUYXC8sGrFNdcD6A/V8pr7a2vCJUrrLdXKYnWF8rhmA8NMIXOr9X7Ar7TD9Wujp9jHrkBgEgeqfpH9wUx6NSVSV/qWEdwJENj3QB7p0JHM=
     procedure_id : 2167461297573410816
     type_code : 15
     code : |1+
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      //[ <Résultat> = ] PanierTotalTaxe ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	chaîne : // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      PROCEDURE PanierTotalTaxe()
      
      renvoyer MonétaireVersChaine(gStPanier.moTaxeTotal)
     type : 458752
   -
     name : PanierValeurHT
     internal_properties : CAAAAAgAAABk2piQTHc9frUYXC8sGrFNdcD6A/V8pr7a2vCJUrrLdXKYnWF8rhmA8NMIXOr9X7Ar7TD9Wujp9jHrkBgEgeqfpH9wUx6NSVSV/qWEdwJENj3QB7p0JHM=
     procedure_id : 2167464948303867655
     type_code : 15
     code : |1+
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      //[ <Résultat> = ] PanierValeurHT ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	chaîne ANSI : // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      PROCEDURE PanierValeurHT()
      
      renvoyer MonétaireVersChaine(gStPanier.moPrixTotalHT)
     type : 458752
   -
     name : CommandeRèglementVersChaîne
     procedure_id : 2167831699855264213
     type_code : 15
     code : |1-
      // Résumé : Permet de récupérer le règlement en lettre à partir de sa valeur
      // Syntaxe :
      //[ <Résultat> = ] CommandeRèglementVersChaîne (<nTypeReglement>)
      //
      // Paramètres :
      //	nTypeReglement : <indiquez ici le rôle de nTypeReglement>
      // Valeur de retour :
      // 	chaîne : // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      PROCEDURE CommandeRèglementVersChaîne(nTypeReglement)
      
      sTypeRèglement est une chaine
      
      SELON nTypeReglement 
      	CAS REGL_CB
      		sTypeRèglement = "CB"
      	CAS REGL_CHEQUE
      		sTypeRèglement = "Chèque"
      	CAS REGL_VIREMENT
      		sTypeRèglement = "Virement bancaire"
      	CAS REGL_PAYPAL
      		sTypeRèglement = "Paypal"
      	AUTRE CAS
      		dbgassertion(faux,"Impossible de trouver le type de règlement")
      		sTypeRèglement = "< Inconnu >"				
      FIN
      
      
      renvoyer sTypeRèglement
     type : 458752
   -
     name : CommandeEtatVersChaine
     procedure_id : 2167832163711839619
     type_code : 15
     code : |1-
      // Résumé : Permet de récupérer l'état de la commande en lettre
      // Syntaxe :
      //[ <Résultat> = ] CommandeEtatVersChaine (<nEtatCommande>)
      //
      // Paramètres :
      //	nEtatCommande : <indiquez ici le rôle de nEtatCommande>
      // Valeur de retour :
      // 	chaîne : // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      PROCEDURE CommandeEtatVersChaine(nEtatCommande)
      
      sEtat est une chaine
      
      selon nEtatCommande
      
      	CAS CDE_ATTENTEREGLEMENT // Commande en attente de règlement
      		sEtat = "En attente de règlement"
      	CAS CDE_PREPARATION // Commande réglée, en cours de préparation
      		sEtat = "En cours de préparation"
      	CAS CDE_LIVRAISON // Commande en cours de livraison
      		sEtat = "En cours de livraison"
      	CAS CDE_LIVREE // Commande livrée
      		sEtat = "Livrée"
      	AUTRE CAS
      		sEtat = "< Inconnu >"		
      FIN
      
      
      renvoyer sEtat
     type : 458752
   -
     name : FraisDePortDefaut
     procedure_id : 2168236861321483875
     type_code : 15
     code : |1-
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      //[ <Résultat> = ] FraisDePortDefaut ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	réel : // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      PROCEDURE FraisDePortDefaut()
      
      selon COL_ECommerce.gStPanier.nTypePort
      	cas 1 // Livraison classique, frais de port de 5 €
      		renvoyer 4.18
      	CAS 2 // Livraison express, frais de port de 15 €
      		RENVOYER 12.54
      	CAS 3 // Livraison immédiate, frais de port de 25 €
      		RENVOYER 20.90
      	AUTRE CAS
      		// on considère que c'est un port classique
      		renvoyer 4.18
      FIN
     type : 458752
   -
     name : InitModulePaiementDefaut
     internal_properties : CAAAAAgAAAAlY+GIffFRnR4v9SbMEaGWSfeoYkQMOJuvnLgp/gu3o3vNa2jl4qKFbEDVrf68FZxrc8KQolvJjQzgdIdbC2RC6a17MgC7we/487etVTBt1wL2BUNdIys93UIM5w0MmWkFFFCKwDAttAJabm5H7gdQ3/nCISEAA7kwhRCfEjp8mv7CFM3yirTmb1GZpf/k2cT0YRxv8qTrruacoEGQ/wN3S1J2n1tCsV3vHANnR65hPV9ufuaysj5HFartKsgbjz89BC5xSRviPCPys+8w7vy/lXTJzvOPN03FfYEylV7oUqVrg7ogvw==
     procedure_id : 2170011627655964065
     type_code : 15
     code : |1+
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      // InitModulePaiementDefaut ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      PROCEDURE InitModulePaiementDefaut()
      // Initialisation du répertoire des données
      ParamètresDonnées(fRepDonnées())
      
      
      // est ce qu'on a déjà fait l'init ?
      si gbInitTerminée = vrai ALORS
      	retour
      FIN
      
      // Initialisation du paiement
      gsURLPaiementOK = "./paiement_valide.awp"
      gsURLPaiementAbandon = "./paiement_abandon.awp"
      gsURLPaiementEchec = "./paiement_echec.awp"
      gsURLRetourBoutique = "./paiement_retour.awp"
      gsPaiementNomObjet = ChaîneConstruit("Panier %1",COL_ECommerce.gsNomSite)
      gsPaiementRefObjet = gsPaiementNomObjet
      
      // Paramètres de connexion au paiements sécurisé
      
      
      // Paramètres communs
      gbPaiementModeSimulation = Vrai
      
      // Paramètres PayBox
      gsPayBoxContrat = "1999888"
      gsPayBoxCodeSociété = 2
      gsPayBoxRang = 99
      
      // Paramètres PayPal
      
      //Pour ce type de paiement sécurisé, Paypal doit fournir les éléments suivant :
      
      // A FAIRE : [PAYPAL] Pour obtenir vos propres codes de test, visitez le site http://developer.paypal.com/
      // Si vous n'avez pas de compte SandBox de test, il faut en créé une pour le mode simulation (différent de votre "vrai" compte PayPal)
      // Mode opératoire au 15/09/2009 :
      // - Allez sur la page http://developer.paypal.com/
      // - Cliquer sur le bouton "Sign UP" et compléter le formulaire
      // - Validez la création de votre compte avec le lien que vous allez recevoir par email
      // - Connectez-vous dans un navigateur à votre compte Sandbox
      // Pour obtenir vos identifiants nécessaire au paiement sécurisé, une fois connecté suivez les étapes suivantes
      // - Allez dans le menu de gauche et choisissez "Api Credentials"
      // - Cliquez sur "Create Test Account"
      // - Choisissez un compte de vendeur (Seller) et Laissez "United States" pour le pays (sinon le type de compte ne permettra pas d'avoir les identifiants)
      // - une fois le compte créé, cliquez anouveau sur "Api Credentials", et la s'affiche les informations :
      // "API Username" => MonPaiement:PayPalIdentifiant
      // "API Password" => MonPaiement:PayPalMotDePasse
      // "Signature"    => MonPaiement:PayPalSignature
      
      
      //!!!! Attention  !!!!
      //En mode simulation il est nécessaire d'avoir un navigateur ouvert sur votre SandBox Paypal de test (http://developer.paypal.com/), en parallèle du test WebDev. 
      //Il doit s'agir d'un navigateur du même type que celui de votre test (il ne faut pas mélanger Internet Explorateur et Firefox par exemple)
      
      //EN CAS D'ERREUR : "La page de retour <http://www.mondomaine.fr/MONSITE_WEB/FR/retourboutique.awp?TEST=O> n'a pas renvoyé le résultat attendu"
      //En cas d'erreur sur le test de la page de retour de paiement sur le serveur sur un problème de résolution d'adresse 
      //il est possible de désactiver le test en mettant la propriété IgnoreTestPageRetour à vrai : 
      //MonPaiement.IgnoreTestPageRetour=vrai
      
      gsPayPalIdentifiant = ""
      gsPayPalMotDePasse = ""
      gsPayPalSignature = ""
      
     type : 458752
   -
     name : CommandeCrée
     internal_properties : CAAAAAgAAAC0oOhgJeS13+l7EyihAMOPto8SkX0eUt2SUq7YccJI4ErUltr7MfGt0LmxuF/I1GhszMArdeFckeVu29kSrkz1y23g/3VNRVeEOKOrfGL0gDMYEdUhcPEGH66hAWAdYwWoGByjkRfCFNlh/Ita0Q9No4KX7bFYI2J2wEPGwoNNHitGp32cSeihep9iXYwpsw==
     procedure_id : 2170049195742797771
     type_code : 15
     code : |1-
      // Résumé : Crée une nouvelle commande avec un numéro séquentiel par jour
      // Syntaxe :
      //[ <Résultat> = ] CommandeCrée ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	chaîne : // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      PROCEDURE CommandeCrée()
      
      // Les numéros générés sont de la forme :
      // BC01-AAAAMMJJ-numseq
      // BC01 : bons de commandes du site web
      // AAAAMMJJ : date de création du bon de commande
      // numseq : numéro séquentiel du BC
      
      // On tente de se positionner sur l'enregistrement du jour et de le bloquer
      tantque HLitRecherchePremier(NumCommande,DateCommande,datesys(),hBlocageEcriture)=Faux
      	// Si on n'a pas trouvé d'enreg, on l'ajoute
      	si htrouve()=Faux ALORS
      		NumCommande.DateCommande = datesys()
      		NumCommande.Numero = 0
      		hajoute(NumCommande)
      	sinon
      		// Enregistrement trouvé mais le blocage n'est pas possible
      		si HErreurBlocage() ALORS
      			continuer
      		sinon
      			// Autre erreur
      			renvoyer ""
      		FIN
      	FIN
      FIN
      
      // Lit le numéro de séquence, l'incrémente et met à jour le fichier
      nNumSequence est un entier = NumCommande.Numero + 1
      NumCommande.Numero++
      si hmodifie(NumCommande)=Faux ALORS
      	gsErreurEnCours = ErreurInfo(errComplet)
      	PageAffiche(page_erreur)	
      FIN
      
      // Génère le numéro de BC
      sNumCommande est une chaine = "BC01-"+DateSys()+NumériqueVersChaîne(nNumSequence,"08d")
      
      // Crée l'enregistrement de commande
      hraz(Commande)
      commande.NumCommande = sNumCommande
      Commande.NumFacture = null
      Commande.IDAdresseFacturation = Null
      Commande.IDAdresseLivraison = Null
      Commande.DateCommande = datesys()
      commande.EtatCommande = CDE_ATTENTEREGLEMENT
      si hajoute(Commande)=Faux ALORS
      	// Echec de l'ajout
      	sNumCommande = ""
      FIN
      
      renvoyer sNumCommande
      
      fin:
      // Dans tous les cas, on débloque le compteur de numéros
      HDébloqueNumEnr(NumCommande,hNumEnrEnCours)
     type : 458752
   -
     name : PanierVersCommande
     procedure_id : 2170073797317133652
     type_code : 15
     code : |1+
      // Résumé : Transforme le panier en cours en une commande ou met à jour la commande
      // Syntaxe :
      // PanierVersCommande ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      PROCEDURE PanierVersCommande()
      
      // Création de la commande ou recherche de celle-ci
      si gStPanier.nIDCommande <> 0 ALORS
      	si HLitRecherchePremier(Commande,IDCommande,gStPanier.nIDCommande)=Faux ALORS
      		// Commande expiré, on détache le panier
      		gStPanier.nIDCommande = 0
      	FIN
      fin
      
      si gStPanier.nIDCommande = 0 alors
      	// Création de la commande
      	si CommandeCrée() = "" ALORS
      		// Si la création de la commande échoue, c'est une erreur fatale pour le site
      		gsErreurEnCours = erreurinfo(errcomplet)
      		PageAffiche(page_erreur)		
      	FIN
      	// A partir de maintenant le panier est relié à une commande
      	gStPanier.nIDCommande = Commande.IDCommande
      FIN
      
      // Supprime les lignes de commande s'il y en a déjà
      REQ_VideCommande.pIDCommande = gStPanier.nIDCommande
      si HExécuteRequête(REQ_VideCommande) = Faux ALORS
      	gsErreurEnCours = ErreurInfo(errComplet)
      	PageAffiche(page_erreur)		
      FIN
      
      // Actualise les valeurs en cache dans le panier
      PanierActualise()
      
      // Création des lignes de commande
      Commande.TotalHT = 0
      Commande.TotalTTC = 0
      pour tout stLigne de gStPanier.tabPanier
      	// Recherche du produit et de son taux de taxe
      	SI HLitRecherchePremier(Produit,Reference,stLigne.sRefProduit)=Faux _OU_ HLitRecherchePremier(TauxTaxe,IDTauxTaxe,Produit.IDTauxTaxe)=Faux ALORS
      		PageAffiche(page_erreur)		
      	FIN
      	
      	// Mise à jour du panier			
      	stLigne.sLibArticle = Produit.LibelleProduit
      	stLigne.moPrixUnitaireHT = Produit.PrixUnitaireHT
      	stLigne.moTaxeUnitaire = (Produit.PrixUnitaireHT * TauxTaxe.Taux) / 100
      	
      	// Création de la ligne de commande
      	hraz(LigneCde)
      	LigneCde.LibelleProduit = Produit.LibelleProduit
      	LigneCde.Quantite = stligne.nNbArticle
      	LigneCde.PrixUnitaireHT = Produit.PrixUnitaireHT
      	LigneCde.TauxTaxe = TauxTaxe.Taux
      	LigneCde.TotalLigne = (stLigne.moPrixUnitaireHT+stLigne.moTaxeUnitaire) * stLigne.nNbArticle
      	LigneCde.IDCommande = gStPanier.nIDCommande
      	LigneCde.Reference = stLigne.sRefProduit
      	si hajoute(LigneCde)=Faux ALORS
      		gsErreurEnCours = ErreurInfo(errComplet)
      		PageAffiche(page_erreur)		
      	FIN
      	
      	Commande.TotalHT += (stLigne.moPrixUnitaireHT*stLigne.nNbArticle)
      	Commande.TotalTTC += LigneCde.TotalLigne
      	
      FIN
      
      Commande.FraisPort = gStPanier.moFraisDeLivraisonTTC
      si hmodifie(Commande)=faux ALORS
      	gsErreurEnCours = ErreurInfo(errComplet)
      	PageAffiche(page_erreur)		
      FIN
     type : 458752
   -
     name : PanierActualise
     procedure_id : 2170082735144257022
     type_code : 15
     code : |1-
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      // PanierActualise ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      PROCEDURE PanierActualise()
      
      POUR TOUT stLigne de gStPanier.tabPanier
      	// Recherche du produit et de son taux de taxe
      	SI HLitRecherchePremier(Produit,Reference,stLigne.sRefProduit)=Faux _OU_ HLitRecherchePremier(TauxTaxe,IDTauxTaxe,Produit.IDTauxTaxe)=Faux ALORS
      		PageAffiche(page_erreur)		
      	FIN
      	// Mise à jour du panier			
      	stLigne.sLibArticle = Produit.LibelleProduit
      	stLigne.moPrixUnitaireHT = Produit.PrixUnitaireHT
      	stLigne.moTaxeUnitaire = (Produit.PrixUnitaireHT * TauxTaxe.Taux) / 100
      FIN
     type : 458752
   -
     name : CommandeModifieClient
     procedure_id : 2170084397298298560
     type_code : 15
     code : |1-
      // Résumé : Associe le client connecté à la commande liée au panier
      // Syntaxe :
      //CommandeModifieClient ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      PROCEDURE CommandeModifieClient()
      
      dbgAssertion(COL_ComptesClient.gstClientEnCours.IDClient<>0,"Il faut être connecté pour utiliser cette fonction")
      dbgAssertion(COL_ECommerce.gStPanier.nIDCommande<>0,"Une commande doit être en cours de traitement pour utiliser cette fonction")
      si COL_ComptesClient.gstClientEnCours.IDClient=0 _ou_ COL_ECommerce.gStPanier.nIDCommande=0 ALORS
      	retour
      FIN
      
      // Se positionne sur la commande
      si HLitRecherchePremier(Commande,IDCommande,COL_ECommerce.gStPanier.nIDCommande,hBlocageEcriture)=Faux ALORS
      	gsErreurEnCours = ErreurInfo(errComplet)
      	PageAffiche(page_erreur)
      FIN
      
      // Réalise l'association
      Commande.IDClient = COL_ComptesClient.gstClientEnCours.IDClient
      hmodifie(Commande)
     type : 458752
   -
     name : CommandeModifieAdresse
     procedure_id : 2170097196305458649
     type_code : 15
     code : |1-
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      // CommandeModifieAdresse ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      PROCEDURE CommandeModifieAdresse()
      
      dbgAssertion(COL_ECommerce.gStPanier.nIDCommande<>0,"Une commande doit être en cours de traitement pour utiliser cette fonction")
      SI COL_ECommerce.gStPanier.nIDCommande=0 ALORS
      	RETOUR
      FIN
      
      // Se positionne sur la commande
      si HLitRecherchePremier(Commande,IDCommande,gStPanier.nIDCommande)=Faux ALORS
      	gsErreurEnCours = erreurinfo(errComplet)
      	PageAffiche(page_erreur)
      FIN
      
      // Si on a une adresse de facturation
      si gStPanier.nIDAdresseFacturation<>0 ALORS
      	// On se positionne dessus
      	si HLitRecherchePremier(Adresse,IDAdresse,gStPanier.nIDAdresseFacturation) ALORS
      		dbgAssertion(Adresse.IDClient=gStPanier.nIDClient,"L'adresse de facturation n'appartient pas au client en cours")
      		// Si l'adresse avait déjà été fixée
      		bModifAdresse est un booleen = Commande.IDAdresseFacturation<>0
      		si bModifAdresse ALORS
      			si HLitRecherchePremier(AdresseArchive,IDAdresse,commande.IDAdresseFacturation)=faux ALORS
      				bModifAdresse = faux
      			FIN
      		FIN
      		// Recopie les valeurs
      		HCopieEnreg(AdresseArchive,Adresse)
      		si bModifAdresse ALORS
      			si hmodifie(AdresseArchive)=Faux ALORS
      				gsErreurEnCours = ErreurInfo(errComplet)
      				PageAffiche(page_erreur)
      			FIN
      		sinon
      			si hajoute(AdresseArchive)=Faux ALORS
      				gsErreurEnCours = ErreurInfo(errComplet)
      				PageAffiche(page_erreur)
      			FIN
      			Commande.IDAdresseFacturation = AdresseArchive.IDAdresse
      			si HModifie(Commande)=Faux ALORS
      				gsErreurEnCours = ErreurInfo(errComplet)
      				PageAffiche(page_erreur)
      			FIN
      		FIN
      	FIN	
      FIN
      
      // Si on a une adresse de livraison
      SI gStPanier.nIDAdresseLivraison<>0 ALORS
      	// On se positionne dessus
      	SI HLitRecherchePremier(Adresse,IDAdresse,gStPanier.nIDAdresseLivraison) ALORS
      		dbgAssertion(Adresse.IDClient=gStPanier.nIDClient,"L'adresse de livraison n'appartient pas au client en cours")
      		// Si l'adresse avait déjà été fixée
      		bModifAdresse = Commande.IDAdresseLivraison<>0
      		SI bModifAdresse ALORS
      			SI HLitRecherchePremier(AdresseArchive,IDAdresse,Commande.IDAdresseLivraison)=Faux ALORS
      				bModifAdresse = Faux
      			FIN
      		FIN
      		// Recopie les valeurs
      		HCopieEnreg(AdresseArchive,Adresse)
      		SI bModifAdresse ALORS
      			SI HModifie(AdresseArchive)=Faux ALORS
      				gsErreurEnCours = ErreurInfo(errComplet)
      				PageAffiche(page_erreur)
      			FIN
      		SINON
      			SI HAjoute(AdresseArchive)=Faux ALORS
      				gsErreurEnCours = ErreurInfo(errComplet)
      				PageAffiche(page_erreur)
      			FIN
      			Commande.IDAdresseLivraison = AdresseArchive.IDAdresse
      			SI HModifie(Commande)=Faux ALORS
      				gsErreurEnCours = ErreurInfo(errComplet)
      				PageAffiche(page_erreur)
      			FIN
      		FIN
      	FIN	
      FIN
     type : 458752
   -
     name : CommandeEmailConfirmationDefaut
     internal_properties : CAAAAAgAAAAgszqkMr6DsddhaXnzuv/eFR370LOmHPWLhme8iz6GpkjzsA/RN8WPHpffwPUAUs6AHrhpy1VcF8eoucmQ9k6jx1MQzethXVqbLUhMPaEZiYr1PpiYdwTB0g8SHl189Ho/taEOQAqpI5igDxc2/sTiLguwNO0Q+htBEQACJ4ajJULxqmPCu17f5MEl1LsdRw==
     procedure_id : 2185672307277947536
     type_code : 15
     code : |1-
      // Résumé : Procédure par défaut d'envoi d'email de confirmation de commande
      // Syntaxe :
      //CommandeEmailConfirmationDefaut (<nIDCommande> est entier [, <nModeReglement> est entier])
      //
      PROCEDURE CommandeEmailConfirmationDefaut( nIDCommande est un entier , nModeReglement est un entier = REGL_CB )
      
      // Passe le time out à 3 seconde (par défaut le serveur smtp est sur localhost, donc s'il met plus de 3 secondes à répondre, il y a un souci)
      EmailChangeTimeOut(3)
      
      // Construction de l'email
      Email.Sujet = ChaîneConstruit("Confirmation de votre commande %1",gsNomSite)
      Email.Expéditeur = gsEmailConfirmationCommande
      Email.NbDestinataire = 1
      
      HLitRecherchePremier(Client,IDClient,COL_ComptesClient.nIDUtilisateur())
      Email.Destinataire[1] = Client.eMail
      
      HLitRecherchePremier(Commande,IDCommande,nIDCommande)
      selon nModeReglement
      	cas REGL_CB,REGL_PAYPAL
      		email.Message = ChaîneConstruit(<§@1e0c492300017dcb0004§>,gsNomSite,MonétaireVersChaine(gStPanier.moPrixTotalTTC),Commande.NumCommande)
      	cas REGL_CHEQUE
       		email.Message = ChaîneConstruit(<§@1e0c492300017dcb0005§>,gsNomSite,MonétaireVersChaine(gStPanier.moPrixTotalTTC),Commande.NumCommande)
      	cas REGL_VIREMENT
       		email.Message = ChaîneConstruit(<§@1e0c492300017dcb0006§>,gsNomSite,MonétaireVersChaine(gStPanier.moPrixTotalTTC),Commande.NumCommande)
      FIN
      
      // Envoi du message
      SI EmailOuvreSessionSMTP(COL_ComptesClient.gsLoginSMTP,COL_ComptesClient.gsMotDePasseSMTP,COL_ComptesClient.gsServeurSMTP,COL_ComptesClient.gnPortSMTP) ALORS
      	EmailEnvoieMessage(COL_ComptesClient.gsLoginSMTP)
      	EmailFermeSession(COL_ComptesClient.gsLoginSMTP)
      FIN
     type : 458752
   -
     name : PanierSupprimeTout
     internal_properties : CAAAAAgAAABwkSrEhxkMocVZtc5/eTt6Kj+5Z6i6c8GvbsdNTphosXnXJ54jVImNG4BDq6DZnXIIa8u4lZX2TkxnszF1rMdJby8AOESomIzCOAiVGJsvthrqT3m8kYKGvI0FMK+KUvvYqM6KlwMzmTvRxipjRA==
     procedure_id : 2185673844885313027
     type_code : 15
     code : |1+
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      // PanierSupprimeTout ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      PROCEDURE PanierSupprimeTout()
      
      // Contenu du panier
      TableauSupprimeTout(gStPanier.tabPanier)
      // Variables "cache" pour limiter le nombre de calculs sur le serveur
      TableauSupprimeTout(gstPanier.taProduit)
      gStPanier.nNbArticle = 0
      gStPanier.moPrixTotalHT = 0
      gStPanier.moTaxeTotal = 0
      // Variables de livraison
      gStPanier.moFraisDeLivraisonHT = 0
      gStPanier.moFraisDeLivraisonTTC = 0
      // Valeur totale du panier TTC, y compris les frais de livraison (montant à payer)
      gStPanier.moPrixTotalTTC = 0
      
      // Variables utilisées par le tunnel de conversion
      // gStPanier.nIDClient  // seule cette variable est conservée (la fin de la commande n'entraine pas la déconnexion)
      gStPanier.nIDAdresseLivraison = 0
      gStPanier.nIDAdresseFacturation = 0
      gStPanier.nIDCommande = 0
     type : 458752
   -
     name : ProduitRecupèrePhoto
     procedure_id : 2186324239871913369
     type_code : 15
     code : |1-
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      //[ <Résultat> = ] ProduitRecupèrePhoto ( [<sRefProduit> est chaîne [, <nIDProduit> est entier [, <nRolePhoto> est entier]]])
      //
      // Paramètres :
      //	sRefProduit (chaîne - valeur par défaut="") : <indiquez ici le rôle de sRefProduit>
      //	nIDProduit (entier - valeur par défaut=-1) : <indiquez ici le rôle de nIDProduit>
      //	nRolePhoto (entier - valeur par défaut=0) : <indiquez ici le rôle de nRolePhoto>
      // Valeur de retour :
      // 	Type indéterminé : // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      PROCEDURE ProduitRecupèrePhoto( local sRefProduit est une chaine = "" , local nIDProduit est un entier = -1 , local nRolePhoto est un entier = ROLE_PHOTO_NORMAL  )
      
      // Recherche par référence
      si nIDProduit=-1 alors
      	HLitRecherchePremier(Produit,Reference,sRefProduit)
      	nIDProduit = Produit.IDProduit
      FIN
      
      // Recherche de la photo correspondant au rôle demandé
      si HLitRecherchePremier(PhotoProduit,IDProduitRole,[nIDProduit,nRolePhoto])=faux alors
      	HLitRecherchePremier(PhotoProduit,IDProduit,nIDProduit)
      FIN
      
      // On vérifie la photo, on considère que tous les chemin que l'on donne sont relatifs
      si PhotoProduit.photo [= "/" ALORS
      	renvoyer PhotoProduit.Photo[[2 a ]]
      FIN
      
      
      renvoyer PhotoProduit.Photo
     type : 458752
   -
     name : CommandeModifieRèglement
     procedure_id : 2245709167865747840
     type_code : 15
     code : |1+
      // Résumé : Permet de modifier le type de règlement
      // Syntaxe :
      //CommandeModifieRèglement (<nRèglement>)
      //
      // Paramètres :
      // 	nRèglement : Type de règlement choisi par le client
      //
      PROCEDURE CommandeModifieRèglement(nRèglement)
      
      
      dbgAssertion(COL_ECommerce.gStPanier.nIDCommande<>0,"Une commande doit être en cours de traitement pour utiliser cette fonction")
      SI COL_ECommerce.gStPanier.nIDCommande=0 ALORS
      	RETOUR
      FIN
      
      // Se positionne sur la commande
      SI HLitRecherchePremier(Commande,IDCommande,gStPanier.nIDCommande)=Faux ALORS
      	gsErreurEnCours = ErreurInfo(errComplet)
      	PageAffiche(page_erreur)
      FIN
      // Modifie le type de règlement
      commande.TypeReglement = nRèglement
      
      // Modifie la commande
      si hmodifie(commande) = faux ALORS
      	gsErreurEnCours = ErreurInfo(errComplet)
      	pageaffiche(page_erreur)
      FIN
     type : 458752
   -
     name : CommandeModifieFraisDePort
     procedure_id : 2245711268111273990
     type_code : 15
     code : |1-
      // Résumé : Permet de modifier les frais de port
      // Syntaxe :
      // CommandeModifieFraisDePort ()
      //
      PROCEDURE CommandeModifieFraisDePort()
      
      
      dbgAssertion(COL_ECommerce.gStPanier.nIDCommande<>0,"Une commande doit être en cours de traitement pour utiliser cette fonction")
      SI COL_ECommerce.gStPanier.nIDCommande=0 ALORS
      	RETOUR
      FIN
      
      // Se positionne sur la commande
      SI HLitRecherchePremier(Commande,IDCommande,gStPanier.nIDCommande)=Faux ALORS
      	gsErreurEnCours = ErreurInfo(errComplet)
      	PageAffiche(page_erreur)
      FIN
      
      // Modifie les frais de port
      Commande.FraisPort = COL_ECommerce.gStPanier.moFraisDeLivraisonTTC
      
      SI PAS HModifie(Commande) ALORS
      	gsErreurEnCours = ErreurInfo(errComplet)
      	PageAffiche(page_erreur)
      FIN
     type : 458752
   -
     name : CommandeModifieEtat
     procedure_id : 2248744459901193986
     type_code : 15
     code : |1-
      // Résumé : permet de modifier l'état de la commande
      // Syntaxe :
      //[ <Résultat> = ] CommandeModifieEtat (<nEtat>)
      //
      // Paramètres :
      //	nEtat : Le nouvel état de la commande
      // Valeur de retour :
      // 	booléen : Vrai si la modification a réussi, faux sinon
      //
      PROCEDURE CommandeModifieEtat(nEtat)
      
      // Si le nouvel état ne fait pas partie de la liste des constantes
      si pas nEtat dans (CDE_ATTENTEREGLEMENT, CDE_LIVRAISON, CDE_LIVREE, CDE_PREPARATION) ALORS
      	renvoyer faux
      FIN
      
      
      HLitRecherchePremier(Commande,IDCommande,gStPanier.nIDCommande)
      si htrouve(commande) = faux ALORS
      	dbgassertion(faux,"Impossible de trouver la commande concernée par ce paiement")
      	renvoyer faux
      FIN
      
      // Modifie l'état de la commande
      Commande.EtatCommande = nEtat
      
      renvoyer hmodifie(commande)
     type : 458752
  procedure_templates : []
  property_templates : []
 code_parameters :
  internal_properties : CAAAAAgAAACVGQgYbalU7DKC3oH4ItDrjtcTHXXV/EuH8q0IbhyQlw==
  original_name : COL_SansNom1
resources :
 string_res :
  identifier : 0x1e0c492300017dcb
  internal_properties : CAAAAAgAAACm76HWfKGWp33VjXInA4cRlqArlgTTA862QGt72W2ld5Y=
  strings :
   -
     text :
      fr-FR : "Vous avez choisi d'effectuer votre paiement par carte bancaire.\r\n\r\nCliquez sur le bouton \"Valider le paiement par Carte Bancaire\" pour être redirigé sur le site de paiement sécurisé.\r\n\r\nAprès le paiement, une page de confirmation de votre commande vous sera présentée et un email de confirmation sera automatiquement envoyé."
     index : 0
   -
     text :
      fr-FR : "Vous avez choisi d'effectuer votre paiement par PayPal.\r\n\r\nCliquez sur le bouton \"Valider le paiement par PayPal\" pour être redirigé sur le site PayPal.\r\n\r\nAprès le paiement, une page de confirmation de votre commande vous sera présentée et un email de confirmation sera automatiquement envoyé."
     index : 1
   -
     text :
      fr-FR : "Vous avez choisi d'effectuer votre paiement par virement bancaire. Nous vous invitons à utiliser les coordonnées bancaires ci-dessous (en précisant bien à votre banque d'effectuer un \"virement OUR\" si vous effectuez un paiement depuis les TOM ou l'étranger).\r\n\r\nCes informations vous seront récapitulées dans votre mail de confirmation de commande.\r\nMerci de préciser dans l'objet de votre virement, le numéro de votre commande.\r\n\r\nCode banque : 30047\r\nCode guichet : 14121\r\nNuméro de compte : 00037236001\r\nClé : 53\r\nDomiciliation : CIC Nantes-Sud\r\n\r\nInformation complémentaires pour les virements OUR :\r\nIBAN : FR76 3004 7141 2100 0372 3600 153\r\nSwift : CMCIFRPP\r\n\r\nVotre commande ne sera traitée par nos services qu'à réception des fonds de votre virement sur notre compte. Si votre règlement ne nous est pas parvenu sous 10 jours, votre commande sera automatiquement annulée."
     index : 2
   -
     text :
      fr-FR : "Vous avez choisi d'effectuer votre paiement par chèque.\r\n\r\nMerci de libeller votre chèque à l'ordre de la société xxx en inscrivant le numéro de votre commande au dos du chèque et de nous l'envoyer à l'adresse suivante :\r\n\r\nxxx - Service Commande\r\nrue yyyy\r\n34000 MONTPELLIER\r\n\r\nDès réception de votre chèque, celui-ci sera traité par nos services.\r\n\r\nSi votre règlement ne nous est pas parvenu sous 10 jours, votre commande sera automatiquement annulée.\r\n"
     index : 3
   -
     text :
      fr-FR : "Bonjour,\r\n\r\nNous vous remercions de votre commande (commande %3 d'un montant de %2 euros) sur le site %1.\r\nVotre paiement a été validé et votre commande va maintenant être prise en charge.\r\n\r\nCordialement,\r\nLe service commande"
     index : 4
   -
     text :
      fr-FR : "Bonjour,\r\n\r\nNous vous remercions de votre commande sur le site %1.\r\nVous avez choisi d'effectuer votre paiement par chèque.\r\n\r\nMerci de libeller votre chèque d'un montant de %2 euros à l'ordre de la société xxx en inscrivant le numéro de votre commande (%3) au dos du chèque et de nous l'envoyer à l'adresse suivante :\r\n\r\nxxx - Service Commande\r\nrue yyyy\r\n34000 MONTPELLIER\r\n\r\nDès réception de votre chèque, celui-ci sera traité par nos services.\r\n\r\nSi votre règlement ne nous est pas parvenu sous 10 jours, votre commande sera automatiquement annulée.\r\n\r\nCordialement,\r\nLe service commande"
     index : 5
   -
     text :
      fr-FR : "Bonjour,\r\n\r\nNous vous remercions de votre commande sur le site %1.\r\n\r\nVous avez choisi d'effectuer votre paiement par virement bancaire. \r\nNous vous invitons à utiliser les coordonnées bancaires ci-dessous (en précisant bien à votre banque d'effectuer un \"virement OUR\" si vous effectuez un paiement depuis les TOM ou l'étranger).\r\n\r\nMerci de préciser dans l'objet de votre virement, le numéro de votre commande (commande %3).\r\nLe montant du virement à effectuer est de %2 euros.\r\n\r\nCode banque : 30047\r\nCode guichet : 14121\r\nNuméro de compte : 00037236001\r\nClé : 53\r\nDomiciliation : CIC Nantes-Sud\r\n\r\nInformation complémentaires pour les virements OUR :\r\nIBAN : FR76 3004 7141 2100 0372 3600 153\r\nSwift : CMCIFRPP\r\n\r\nVotre commande ne sera traitée par nos services qu'à réception des fonds de votre virement sur notre compte. Si votre règlement ne nous est pas parvenu sous 10 jours, votre commande sera automatiquement annulée.\r\n\r\nCordialement,\r\nLe service commande"
     index : 6
custom_note :
 internal_properties : CAAAAAgAAABtB9HWVzrXO2+4NDRVK0vmzaNKrCKqH1DBX30lMmGZ
